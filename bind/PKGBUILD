# Maintainer: Gore Liu <goreliu@126.com>

pkgbase=bind
pkgname=(bind bind-tools)
_pkgver=9.10.3-P2
pkgver=${_pkgver//-/.}
pkgrel=1
url='http://www.isc.org/software/bind/'
license=('custom:ISC')
arch=('i686' 'x86_64')
options=('!emptydirs')
makedepends=('libxml2-devel' 'zlib-devel' 'openssl-devel' 'libreadline-devel'
 'python')
source=("http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz"
        'tmpfiles.conf'
        'sysusers.conf'
        'named.conf'
        'named.service'
        'localhost.zone'
        'localhost.ip6.zone'
        '127.0.0.zone'
        'empty.zone')
sha1sums=('c23663a9b464cff6f2279e80513ef97f9fe5803b'
          'c5a2bcd9b0f009ae71f3a03fbdbe012196962a11'
          '9537f4835a1f736788d0733c7996a10db2d4eee4'
          'c017aae379c32c7cb1aa1ad84776b83e3a5c139f'
          'cb2e81b4cbf9efafb3e81e3752f0154e779cc7ec'
          '6704303a6ed431a29b1d8fe7b12decd4d1f2f50f'
          '52da8f1c0247a11b16daa4e03d920e8f09315cbe'
          '9c33726088342207ad06d33b2c13408290a0c8ad'
          '4f4457b310cbbeadca2272eced062a9c2b2b42fe')

prepare() {
  msg2 'Getting a fresh version of root DNS'
  # no more using source array, lack of versioning.
  curl -o root.hint http://www.internic.net/zones/named.root
  [[ -s root.hint ]]
}

build() {
  cd bind-$_pkgver
  export CFLAGS+=' -DDIG_SIGCHASE'
  ./configure \
    --build=x86_64-pc-msys \
    --without-dlopen \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --disable-static \
    --with-python=/usr/bin/python \
    --with-ipv6 \
    --with-openssl \
    --with-libxml2 \
    --with-libtool
  make
}

package_bind() {
  pkgdesc='The ISC DNS Server'
  provides=('dns-server')
  depends=('libxml2' 'openssl' 'bind-tools')
  backup=('etc/named.conf'
          'var/named/127.0.0.zone'
          'var/named/localhost.zone'
          'var/named/localhost.ip6.zone'
          'var/named/empty.zone')

  cd "bind-$_pkgver"
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  for _d in bin/{check,confgen,named,rndc}; do
    (cd "$_d" && make DESTDIR="$pkgdir" install)
  done

  cd "$srcdir"
  install -D -m644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  install -D -m644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  install -D -m644 named.service "$pkgdir/usr/lib/systemd/system/named.service"
  install -D -m640 named.conf "$pkgdir/etc/named.conf"

  install -d       "$pkgdir/var/named"
  install    -m640 root.hint "$pkgdir/var/named"
  install    -m640 localhost.zone "$pkgdir/var/named"
  install    -m640 localhost.ip6.zone "$pkgdir/var/named"
  install    -m640 127.0.0.zone "$pkgdir/var/named"
  install    -m640  empty.zone "$pkgdir/var/named"
}

package_bind-tools() {
  pkgdesc='The ISC DNS tools'
  depends=('libxml2' 'zlib' 'openssl' 'libreadline')
  optdepends=('python: for python scripts')

  cd "bind-$_pkgver"
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  make DESTDIR="$pkgdir" SUBDIRS="" install
  (cd lib && make DESTDIR="$pkgdir" install)

  sed -i 's/\.exe//g' bin/python/Makefile
  for _d in bin/{dig,dnssec,delv,nsupdate,python,tools}; do
    (cd "$_d" && make DESTDIR="$pkgdir" install)
  done
}

# vim:set ts=2 sw=2 et:
