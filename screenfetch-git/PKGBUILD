# Maintainer: Gore Liu <goreliu@126.com>

pkgname=screenfetch-git
_pkgname=screenfetch
pkgver=3.7.0.r44.g22e5bee
pkgrel=1
pkgdesc="CLI Bash script to show system/theme info in screenshots"
arch=('any')
url="https://github.com/KittyKatt/screenFetch"
license=('GPL')
provides=($_pkgname)
conflicts=($_pkgname)
depends=('bash')
makedepends=('git')
source=("$pkgname::git+https://github.com/KittyKatt/screenFetch.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags --long | sed -r -e 's,^[^0-9]*,,;s,([^-]*-g),r\1,;s,[-_],.,g'
}

build() {
  cd $pkgname
  sed -i 's/Cygwin/Msys/g' screenfetch-dev
  sed -i 's/cygwin/msys/g' screenfetch-dev
  sed -i 's/ \/v / \/\/v /g' screenfetch-dev
  sed -i "s/'Arch Linux'|/'Arch Linux'|'Msys'|/g" screenfetch-dev
}

package() {
  cd $pkgname

  install -Dm755 "screenfetch-dev" "$pkgdir/usr/bin/screenfetch"
  install -Dm644 "screenfetch.1" "$pkgdir/usr/share/man/man1/screenfetch.1"
}
