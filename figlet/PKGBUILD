# Maintainer: Gore Liu <goreliu@126.com>

pkgname=figlet
pkgver=2.2.5
pkgrel=2
pkgdesc="A program for making large letters out of ordinary text"
arch=('i686' 'x86_64')
url="http://www.figlet.org/"
license=('BSD')
source=(ftp://ftp.figlet.org/pub/figlet/program/unix/${pkgname}-${pkgver}.tar.gz)
sha1sums=('dda696958c161bd71d6590152c94c4f705415727')

build() {
  cd ${pkgname}-${pkgver}
  sed -i 's/-DTLF_FONTS/# -DTLF_FONTS/g' Makefile
  make DEFAULTFONTDIR=/usr/share/figlet/fonts all
}

check() {
  cd ${pkgname}-${pkgver}
  # TLF_FONTS disabled
  make check || true
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" BINDIR=/usr/bin MANDIR=/usr/share/man \
    DEFAULTFONTDIR=/usr/share/figlet/fonts install 
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
